# zshrc sets up aliases, bindings, and anything else that's only needed for
# interactive shells.  This implementation is a stub that sets up basic options
# that everything else depends on, then reads the rest of the configuration from
# the "scripts" directory.

if [[ $UID != 0 && -z $SUDO_USER ]]; then
  # Private temp dir.
  SVTMP=$TMPPREFIX-$USERNAME-sv
  if [[ -e $SVTMP ]]; then
    if [[ ! ( -O $SVTMP && -d $SVTMP ) ]]; then
      echo "Bad $SVTMP"
      unset SVTMP
    fi
  else
    if mkdir -pm 700 "$SVTMP" 2> /dev/null; then
      echo "Created $SVTMP"
    elif [[ -O $SVTMP && -d $SVTMP ]]; then
      echo "Another process created $SVTMP for us, yay"
    else
      echo "Unable to create $SVTMP"
      unset SVTMP
    fi
  fi

  setopt hist_ignore_all_dups

  # Pick up the rest of the configuration.
  for rc in $SVCONFIGDIR/zsh/scripts/*.zsh; do
    source $rc
  done
else
  # Minimal configuration for sudo shells.
  if [[ $UID != 0 ]]; then
    PROMPT="<$USER> $HOST [%/]%# "
  else
    PROMPT="$HOST [%/]%# "
  fi
  alias v=vi
  alias vv='vi -R -'
fi
