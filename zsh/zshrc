# zshrc sets up aliases, bindings, and anything else that's only needed for
# interactive shells.

alias aptup='sudo apt-get update; sudo apt-get dist-upgrade'
[ "$SVPLATFORM" = osx ] && alias top='top -o cpu'
gman () { man $* | col -b | gview - }
srcgrep () {
  grep -r "$*" . | grep -v tags | grep -v \\.svn| grep -v \\.git \
      | grep -v "Binary file"
}

bindkey -e
bindkey '^u' backward-kill-line
bindkey '\eq' push-line-or-edit

# Beeping is rude.
setopt no_beep

# Retain literal '*' when there's no glob match.  This is sloppy but useful.
setopt no_nomatch

HISTFILE=$HOME/.history
HISTSIZE=10100
SAVEHIST=10000
setopt inc_append_history  # fc -RI to import from other shells.

# Private temp dir.
SVTMP="$TMPPREFIX-$USERNAME-sv"
if [ -e "$SVTMP" ]; then
  if [[ ! ( -O "$SVTMP" && -d "$SVTMP" ) ]]; then
    echo "Bad $SVTMP"
    unset SVTMP
  fi
else
  if mkdir -pm 700 "$SVTMP" 2> /dev/null; then
    echo "Created $SVTMP"
  elif [[ -O "$SVTMP" && -d "$SVTMP" ]]; then
    echo "Another process created $SVTMP for us, yay"
  else
    echo "Unable to create $SVTMP"
    unset SVTMP
  fi
fi

# Dot dee!
for rc in $SVCONFIGDIR/zsh/zshrc.d/*.zsh; do
  source $rc
done

# But wait--there's more (maybe)!
[ "$SVPLATFORM" = cygwin ] && source "$SVCONFIGDIR/cygwin/zshrc"
